//
// Function Name: vimAuthenticationBarracudaWAF
// Description: This ASIM parser supports normalizing the Barracuda WAF logs to the ASIM authentication normalized schema.

// Version: 
// Last Updated: 
//
.create-or-alter function with (skipvalidation=true) vimAuthenticationBarracudaWAF(    ['starttime']:datetime=datetime(null),
    ['endtime']:datetime=datetime(null),
    ['username_has_any']:dynamic=dynamic([]),
    ['targetappname_has_any']:dynamic=dynamic([]),
    ['srcipaddr_has_any_prefix']:dynamic=dynamic([]),
    ['srchostname_has_any']:dynamic=dynamic([]),
    ['eventtype_in']:dynamic=dynamic([]),
    ['eventresultdetails_in']:dynamic=dynamic([]),
    ['eventresult']:string='*',
    ['disabled']:bool=False)
{
let barracudaSchema = datatable(
  LogType_s: string,
  UnitName_s: string,
  EventName_s: string,
  DeviceReceiptTime_s: string,
  HostIP_s: string,
  host_s: string,
  LoginIP_s: string,
  Severity_s: string,
  LoginPort_d: real,
  AdminName_s: string,
  EventMessage_s: string,
  TimeTaken_d: real,
  TenantId: string,
  Message: string,
  SourceSystem: string,
  _ResourceId: string,
  RawData: string,
  Computer: string,
  MG: string,
  ManagementGroupName: string,
  SourceIP: string,
  TimeGenerated: datetime
  )[];
  let SeverityLookup = datatable (severity: int, EventSeverity: string)
      [
      0, "High", 
      1, "High", 
      2, "High", 
      3, "Medium",
      4, "Low",
      5, "Low", 
      6, "Informational",
      7, "Informational" 
  ];
  let EventTypeLookup = datatable (
      EventName_s: string,
      EventType_lookup: string,
      EventResult: string
  )
      [
      "LOGIN", "Logon", "Success",
      "UNSUCCESSFUL_LOGIN", "Logoff", "Failure",
      "LOGOUT", "Logoff", "Success"
  ];
  let EventResultDetailsLookup = datatable (
      Reason: string,
      EventResultDetails: string
  )
      [
      "Invalid Username/Password", "Incorrect password",
      "Account Lockout", "User locked",
      "Expired or Disabled Accounts", "User disabled",
      "IP Blocking", "Logon violates policy",
      "Session Timeouts", "Session expired",
      "CAPTCHA Verification", "Other"
  ];
  let parser = (
      starttime: datetime=datetime(null), 
      endtime: datetime=datetime(null), 
      username_has_any: dynamic = dynamic([]),
      targetappname_has_any: dynamic = dynamic([]),
      srcipaddr_has_any_prefix: dynamic = dynamic([]),
      srchostname_has_any: dynamic = dynamic([]),
      eventtype_in: dynamic = dynamic([]),
      eventresultdetails_in: dynamic = dynamic([]),
      eventresult: string = '*',
      disabled: bool=false
      ) { 
      let BarracudaCustom = 
          union isfuzzy=true
              barracudaSchema,
              barracuda_CL
          | where not(disabled)
              and (LogType_s == "AUDIT")
              and (EventName_s in ("LOGIN", "LOGOUT", "UNSUCCESSFUL_LOGIN"))
          | where (isnull(starttime) or TimeGenerated >= starttime)
              and (isnull(endtime) or TimeGenerated <= endtime)
              and ((array_length(username_has_any) == 0) or (AdminName_s has_any (username_has_any)))
              and (array_length(targetappname_has_any) == 0) // TargetAppName not available in source
              and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(LoginIP_s, srcipaddr_has_any_prefix))
              and (array_length(srchostname_has_any) == 0) // SrcHostname not available in source
          // Filtering for eventtype_in done later in the parser
          // Filtering for eventresultdetails_in done later in the parser
          // Filtering for eventresult done later in the parser
          | parse trim(@'[^\w(")]+', EventMessage_s) with * "Reason=" Reason: string
          | extend Reason = trim(@'(")', Reason)
          | lookup EventResultDetailsLookup on Reason
          // Filtering on eventresultdetails_in
          | where (array_length(eventresultdetails_in) == 0 or EventResultDetails in~ (eventresultdetails_in))
          | lookup EventTypeLookup on EventName_s
          | extend 
              EventType = EventType_lookup,
              severity = toint(Severity_s)
          // Filtering on eventtype_in and eventresult
          | where ((array_length(eventtype_in) == 0) or EventType in~ (eventtype_in))
              and (eventresult == "*" or (EventResult == eventresult))
          | lookup SeverityLookup on severity
          | extend
              Dvc = UnitName_s,
              EventCount = toint(1),
              EventProduct = "WAF",
              EventSchema = "Authentication",
              EventSchemaVersion = "0.1.3",
              EventVendor = "Barracuda"
          | extend
              SrcPortNumber = toint(LoginPort_d),
              DvcIpAddr = HostIP_s,
              SrcIpAddr = LoginIP_s,
              DvcHostname = host_s,
              ActorUsername = AdminName_s,
              EventStartTime = iff(isnotempty(TimeTaken_d), unixtime_milliseconds_todatetime(tolong(DeviceReceiptTime_s) - tolong(TimeTaken_d)), unixtime_milliseconds_todatetime(tolong(DeviceReceiptTime_s)))
          // mapping ASimMatchingUsername
          | extend temp_isMatchActorUsername=ActorUsername has_any(username_has_any)
          // TargetUsername not coming from source. Hence, not mapped.
          | extend ASimMatchingUsername = case(
                                      array_length(username_has_any) == 0,
                                      "-",
                                      temp_isMatchActorUsername,
                                      "ActorUsername",
                                      "No match"
                                  )
          | extend
              ActorUsernameType = iff(isnotempty(ActorUsername), "Simple", ""),
              ActorUserType = iff(isnotempty(ActorUsername), "Admin", ""),
              EventEndTime = EventStartTime
          | extend
              IpAddr = SrcIpAddr,
              Src = SrcIpAddr
          | project-away
              *_s,
              *_d,
              temp_*,
              severity,
              EventType_lookup,
              TenantId,
              Message,
              SourceSystem,
              _ResourceId,
              RawData,
              Computer,
              MG,
              ManagementGroupName,
              SourceIP,
              Reason;
      let BarracudaCEF = 
          CommonSecurityLog
          | where not(disabled)
              and DeviceVendor startswith "Barracuda"
              and (DeviceProduct == "WAF" or DeviceProduct == "WAAS")
          | where DeviceEventCategory == "AUDIT"
              and (toupper(ProcessName) in ("LOGIN", "LOGOUT", "UNSUCCESSFUL_LOGIN"))
          | where (isnull(starttime) or TimeGenerated >= starttime)
              and (isnull(endtime) or TimeGenerated <= endtime)
              and ((array_length(username_has_any) == 0) or (DestinationUserName has_any (username_has_any)))
              and (array_length(targetappname_has_any) == 0) // TargetAppName not available in source
              and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(SourceIP, srcipaddr_has_any_prefix))
              and (array_length(srchostname_has_any) == 0) // SrcHostname not available in source
          // Filtering for eventtype_in done later in the parser
          // Filtering for eventresultdetails_in done later in the parser
          // Filtering for eventresult done later in the parser
          | parse trim(@'[^\w(")]+', Message) with * "Reason=" Reason: string
          | extend Reason = trim(@'(")', Reason)
          | lookup EventResultDetailsLookup on Reason
          // Filtering on eventresultdetails_in
          | where (array_length(eventresultdetails_in) == 0 or EventResultDetails in~ (eventresultdetails_in))
          | extend ProcessName = toupper(ProcessName)
          | lookup EventTypeLookup on $left.ProcessName == $right.EventName_s
          | extend 
              EventType = EventType_lookup,
              severity = toint(LogSeverity)
          // Filtering on eventtype_in and eventresult
          | where ((array_length(eventtype_in) == 0) or EventType in~ (eventtype_in))
          | lookup SeverityLookup on severity
          | extend
              Dvc = DeviceName,
              EventCount = toint(1),
              EventProduct = "WAF",
              EventSchema = "Authentication",
              EventSchemaVersion = "0.1.3",
              EventVendor = "Barracuda"
          | extend
              SrcPortNumber = toint(SourcePort),
              DvcIpAddr = DeviceAddress,
              SrcIpAddr = SourceIP,
              DvcHostname = DeviceName,
              ActorUsername = DestinationUserName,
              EventStartTime = iff(isnotempty(FlexNumber2), unixtime_milliseconds_todatetime(tolong(ReceiptTime) - tolong(FlexNumber2)), unixtime_milliseconds_todatetime(tolong(ReceiptTime)))
          // mapping ASimMatchingUsername
          | extend temp_isMatchActorUsername=ActorUsername has_any(username_has_any)
          // TargetUsername not coming from source. Hence, not mapped.
          | extend ASimMatchingUsername = case(
                                      array_length(username_has_any) == 0,
                                      "-",
                                      temp_isMatchActorUsername,
                                      "ActorUsername",
                                      "No match"
                                  )
          | extend
              ActorUsernameType = iff(isnotempty(ActorUsername), "Simple", ""),
              ActorUserType = iff(isnotempty(ActorUsername), "Admin", ""), 
              EventEndTime = EventStartTime
          | extend
              IpAddr = SrcIpAddr,
              Src = SrcIpAddr
          | project-away
              ThreatConfidence,
              EventType_lookup,
              CommunicationDirection,
              AdditionalExtensions,
              Device*,
              Source*,
              Destination*,
              temp_*,
              Activity,
              LogSeverity,
              ApplicationProtocol,
              ProcessID,
              ExtID,
              Protocol,
              Reason,
              ReceiptTime,
              SimplifiedDeviceAction,
              OriginalLogSeverity,
              ProcessName,
              EndTime,
              ExternalID,
              File*,
              ReceivedBytes,
              Message,
              Old*,
              EventOutcome,
              Request*,
              StartTime,
              Field*,
              Flex*,
              Remote*,
              Malicious*,
              severity,
              ThreatSeverity,
              IndicatorThreatType,
              ThreatDescription,
              _ResourceId,
              SentBytes,
              ReportReferenceLink,
              Computer,
              TenantId;
      union isfuzzy = true 
          BarracudaCustom,
          BarracudaCEF
  };
  parser (
    starttime=starttime,
    endtime=endtime,
    username_has_any=username_has_any,
    targetappname_has_any=targetappname_has_any,
    srcipaddr_has_any_prefix=srcipaddr_has_any_prefix,
    srchostname_has_any=srchostname_has_any,
    eventtype_in=eventtype_in,
    eventresultdetails_in=eventresultdetails_in,
    eventresult=eventresult,
    disabled=disabled
)
}



//
// Function Name: vimAuthenticationCiscoASA
// Description: This ASIM parser supports normalizing authentication events, collected from Cisco ASA devices, to the ASIM Authentication schema.

// Version: 
// Last Updated: 
//
.create-or-alter function with (skipvalidation=true) vimAuthenticationCiscoASA(    ['starttime']:datetime=datetime(null),
    ['endtime']:datetime=datetime(null),
    ['username_has_any']:dynamic=dynamic([]),
    ['targetappname_has_any']:dynamic=dynamic([]),
    ['srcipaddr_has_any_prefix']:dynamic=dynamic([]),
    ['srchostname_has_any']:dynamic=dynamic([]),
    ['eventtype_in']:dynamic=dynamic([]),
    ['eventresultdetails_in']:dynamic=dynamic([]),
    ['eventresult']:string='*',
    ['disabled']:bool=False)
{
let parser = (
 starttime: datetime=datetime(null), 
 endtime: datetime=datetime(null), 
 username_has_any: dynamic = dynamic([]),
 targetappname_has_any: dynamic = dynamic([]),
 srcipaddr_has_any_prefix: dynamic = dynamic([]),
 srchostname_has_any: dynamic = dynamic([]),
 eventtype_in: dynamic = dynamic([]),
 eventresultdetails_in: dynamic = dynamic([]),
 eventresult: string = '*',
 disabled: bool=false
 ) {
  let DeviceEventClassIDLookup = datatable (
 DeviceEventClassID: string,
 EventResultDetails: string,
 EventType: string,
 EventResult: string,
 DvcAction: string,
 EventSubType: string
 )
         [
     "113004", "", "Logon", "Success", "Allowed", "Remote",
     "113005", "Incorrect password", "Logon", "Failure", "Blocked", "Remote",
     "113006", "Logon violates policy", "Logon", "Failure", "Blocked", "Remote",
     "113008", "", "Logon", "Success", "Allowed", "Remote",
     "113010", "", "Logon", "Success", "Allowed", "Remote",
     "113012", "", "Logon", "Success", "Allowed", "Remote",
     "113019", "", "Logoff", "Success", "Allowed", "",
     "113039", "", "Logon", "Success", "Allowed", "Remote",
     "315011", "", "Logoff", "Success", "Allowed", "",
     "502103", "", "Elevate", "Success", "Allowed", "AssumeRole",
     "605004", "Other", "Logon", "Failure", "Blocked", "Remote",
     "605005", "", "Logon", "Success", "Allowed", "Remote",
     "611101", "", "Logon", "Success", "Allowed", "Remote",
     "611102", "Other", "Logon", "Failure", "Blocked", "Remote",
     "611103", "", "Logoff", "Success", "Allowed", "",
     "713198", "Logon violates policy", "Logon", "Failure", "Blocked", "Remote",
     "716002", "", "Logoff", "Success", "Allowed", "",
     "716038", "", "Logon", "Success", "Allowed", "Remote",
     "716039", "Other", "Logon", "Failure", "Blocked", "Remote",
     "716040", "Other", "Logon", "Failure", "Blocked", "Remote",
     "722022", "", "Logon", "Success", "Allowed", "Remote",
     "722023", "", "Logoff", "Success", "Allowed", "",
     "722028", "", "Logoff", "Success", "Allowed", "",
     "722037", "", "Logoff", "Success", "Allowed", "",
     "772002", "", "Logon", "Success", "Allowed", "",
     "772003", "Other", "Logon", "Failure", "Blocked", "",
     "772004", "Other", "Logon", "Failure", "Blocked", "",
     "772005", "", "Logon", "Success", "Allowed", "",
     "772006", "Other", "Logon", "Failure", "Blocked", ""
 ];
     let FilteredDeviceEventClassID = toscalar(
         DeviceEventClassIDLookup 
         | summarize make_set(DeviceEventClassID)
         );
     let SeverityLookup = datatable (EventOriginalSeverity: string, EventSeverity: string)
         [
     "1", "High", // Alert,
     "2", "High", // Critical
     "3", "Medium", // Error
     "4", "Low", // Warning
     "5", "Informational", // Notification
     "6", "Informational", // Information
     "7", "Informational", // Debug
 ];
     let LogMessages = 
         CommonSecurityLog 
         | where not(disabled)
         | where
             (isnull(starttime) or TimeGenerated >= starttime) and
             (isnull(endtime) or TimeGenerated <= endtime) 
         | where DeviceVendor =~ "Cisco"
             and DeviceProduct == "ASA"
             and DeviceEventClassID in(FilteredDeviceEventClassID)
             and ((array_length(username_has_any) == 0) or (Message has_any (username_has_any)))
             and (array_length(targetappname_has_any) == 0) // TargetAppName not available in source
             and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(Message, srcipaddr_has_any_prefix))
             and (array_length(srchostname_has_any) == 0) // SrcHostname not available in source
         // eventtype_in filtering done later in the parser
         // eventresultdetails_in filtering done later in the parser
         // eventresult filtering done later in the parser
         | extend EventOriginalSeverity = tostring(split(Message, "-", 1)[0])
         | lookup SeverityLookup on EventOriginalSeverity
         | project
             TimeGenerated,
             Type,
             Computer,
             _ItemId,
             DeviceEventClassID,
             Message,
             DeviceAddress,
             EventOriginalSeverity,
             EventSeverity
         | lookup DeviceEventClassIDLookup on DeviceEventClassID
         // Filtering on eventtype_in and eventresult
         | where ((array_length(eventtype_in) == 0) or (EventType in~ (eventtype_in)))
             and (eventresult == "*" or (EventResult == eventresult));
     union 
         (
         LogMessages
         | where DeviceEventClassID == 113005
         | parse Message with * 'reason = ' EventOriginalResultDetails ' : server = ' TargetIpAddr ' ' * 'user = ' TargetUsername ' ' * 'user IP = ' SrcIpAddr
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID == 502103
         | parse Message with * "Uname: " TargetUsername " " *
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID in(605004, 605005)
         | parse Message with * 'from ' SrcIpAddr '/' SrcPortNumber: int " to " * ":" TargetIpAddr '/' * 'user "' TargetUsername '"'
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID in(611101, 611102)
         | parse Message with * 'IP address: ' SrcIpAddr ', Uname: ' TargetUsername
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID == 611103
         | parse Message with * ' Uname: ' TargetUsername
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID == 113004
         | parse Message with * 'server = ' TargetIpAddr ' ' * 'user = ' TargetUsername
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID in(113008, 113012)
         | parse Message with * 'user = ' TargetUsername
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID == 113019
         | parse Message with * 'Username = ' TargetUsername ', IP = ' SrcIpAddr ',' * 
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID in(113039, 716002, 716039, 722022, 722023, 722028, 722037)
         | parse Message with * '> User <' TargetUsername "> IP <" SrcIpAddr ">" *
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID == 315011
         | parse Message with * 'from ' SrcIpAddr ' ' * 'user "' TargetUsername '" ' * ' reason: "' EventOriginalResultDetails '" ' *
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix))
         | extend EventResultDetails = iif(EventOriginalResultDetails == "Internal error", "Other", EventResultDetails)
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID == 113010
         | parse Message with * 'user ' TargetUsername ' from  server' SrcIpAddr
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID == 113006
         | parse Message with * 'User ' TargetUsername ' locked' *
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID == 716040
         | parse Message with * 'Denied ' TargetUsername ' login' *
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID == 713198
         | parse Message with * 'Failed: ' TargetUsername ' User' *
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID == 716038
         | parse Message with * 'User ' TargetUsername ' IP ' SrcIpAddr ' Authentication'*
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID in(772002)
         | parse Message with * 'user ' TargetUsername ', cause: ' EventOriginalResultDetails
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0))
         | project-away Message
         ),
         (
         LogMessages
         | where DeviceEventClassID in(772003, 772004)
         | parse Message with * 'user ' TargetUsername ', IP ' SrcIpAddr ', cause: ' EventOriginalResultDetails
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix))
         | project-away Message
         ), 
         (
         LogMessages
         | where DeviceEventClassID in(772005)
         | parse Message with * 'user ' TargetUsername ' passed'
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0))
         | project-away Message
         ),  
         (
         LogMessages
         | where DeviceEventClassID in(772006)
         | parse Message with * 'user ' TargetUsername ' failed'
         | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
             and ((array_length(srcipaddr_has_any_prefix) == 0))
         | project-away Message
         )
     // mapping ASimMatchingUsername
     | extend temp_isMatchTargetUsername=TargetUsername has_any(username_has_any)
     // ActorUsername not coming from source. Hence, not mapped.
     | extend ASimMatchingUsername = case
         (
                                     array_length(username_has_any) == 0,
                                     "-",
                                     temp_isMatchTargetUsername,
                                     "TargetUsername",
                                     "No match"
                                 )
     | project-rename 
         DvcHostname           = Computer,
         EventUid              = _ItemId,
         EventOriginalType     = DeviceEventClassID,
         DvcIpAddr             = DeviceAddress
     | extend 
         EventSchemaVersion = "0.1.3",
         EventSchema        = "Authentication",
         EventVendor        = "Cisco",
         EventProduct       = "ASA",
         EventCount         = int(1),
         EventStartTime     = TimeGenerated,
         EventEndTime       = TimeGenerated,
         Dvc                = DvcHostname,
         User               = TargetUsername,
         Src                = SrcIpAddr,
         IpAddr             = SrcIpAddr,
         Dst                = TargetIpAddr,
         TargetUsernameType = _ASIM_GetUsernameType(TargetUsername),
         EventResultDetails = iif(TargetUsername == "*****", "No such user or password", EventResultDetails)
     // filtering on 'eventresultdetails_in'
     | where (array_length(eventresultdetails_in) == 0 or EventResultDetails in~ (eventresultdetails_in))
   };
 parser (
     starttime=starttime,
     endtime=endtime,
     username_has_any=username_has_any,
     targetappname_has_any=targetappname_has_any,
     srcipaddr_has_any_prefix=srcipaddr_has_any_prefix,
     srchostname_has_any=srchostname_has_any,
     eventtype_in=eventtype_in,
     eventresultdetails_in=eventresultdetails_in,
     eventresult=eventresult,
     disabled=disabled
 )
}



//
// Function Name: vimAuthenticationCiscoISE
// Description: This ASIM parser supports normalizing Cisco ISE events produced by the Microsoft Sentinel Cisco ISE connector to the ASIM Authentication schema.

// Version: 
// Last Updated: 
//
.create-or-alter function with (skipvalidation=true) vimAuthenticationCiscoISE(    ['starttime']:datetime=datetime(null),
    ['endtime']:datetime=datetime(null),
    ['username_has_any']:dynamic=dynamic([]),
    ['targetappname_has_any']:dynamic=dynamic([]),
    ['srcipaddr_has_any_prefix']:dynamic=dynamic([]),
    ['srchostname_has_any']:dynamic=dynamic([]),
    ['eventtype_in']:dynamic=dynamic([]),
    ['eventresultdetails_in']:dynamic=dynamic([]),
    ['eventresult']:string='*',
    ['disabled']:bool=False)
{
let EventFieldsLookup=datatable(
  EventOriginalType: string,
  EventType: string,
  EventOriginalSeverity: string,
  EventResult: string,
  EventSeverity: string,
  EventResultDetails: string,
  EventMessage: string,
  EventOriginalResultDetails: string
)[
    "25104", "Logon", "DEBUG", "Success", "Informational", "", "Plain text password authentication in external REST ID store server succeeded", "Plain text password authentication in external REST ID store server succeeded",
    "25105", "Logon", "DEBUG", "Failure", "Low", "No such user or password", "Plain text password authentication in external REST ID store server failed", "Plain text password authentication in external REST ID store server failed",
    "25106", "Logon", "DEBUG", "Failure", "Low", "No such user or password", "REST ID Store server indicated plain text password authentication failure", "REST ID store server indicated plain text password authentication failure",
    "25112", "Logon", "DEBUG", "Failure", "Low", "No such user or password", "REST database indicated plain text password authentication failure", "REST database indicated plain text password authentication failure",
    "51000", "Logon", "NOTICE", "Failure", "Low", "No such user or password", "Administrator authentication failed", "Administrator authentication failed",
    "51001", "Logon", "NOTICE", "Success", "Informational", "", "Administrator authentication succeeded", "Administrator authentication succeeded",
    "51002", "Logoff", "NOTICE", "Success", "Informational", "", "Administrator logged off", "Administrator logged off",
    "51003", "Logoff", "NOTICE", "Success", "Informational", "Session expired", "Session Timeout", "Administrator had a session timeout",
    "51004", "Logon", "NOTICE", "Failure", "Low", "Logon violates policy", "Rejected administrator session from unauthorized client IP address", "An attempt to start an administration session from an unauthorized client IP address was rejected. Check the client's administration access setting.",
    "51005", "Logon", "NOTICE", "Failure", "Low", "User disabled", "Administrator authentication failed. Administrator account is disabled", "Administrator authentication failed. Administrator account is disabled.",
    "51006", "Logon", "NOTICE", "Failure", "Low", "User disabled", "Administrator authentication failed. Account is disabled due to inactivity", "Administrator authentication failed. Account is disabled due to inactivity.",
    "51007", "Logon", "NOTICE", "Failure", "Low", "User disabled", "Authentication failed. Account is disabled due to password expiration", "Authentication failed. Account is disabled due to password expiration",
    "51008", "Logon", "NOTICE", "Failure", "Low", "Logon violates policy", "Administrator authentication failed. Account is disabled due to excessive failed authentication attempts", "Administrator authentication failed. Account is disabled due to excessive failed authentication attempts.",
    "51009", "Logon", "NOTICE", "Failure", "Low", "Other", "Authentication failed. ISE Runtime is not running", "Authentication failed. ISE Runtime is not running",
    "51020", "Logon", "NOTICE", "Failure", "Low", "No such user", "Administrator authentication failed. Login username does not exist.", "Administrator authentication failed. Login username does not exist.",
    "51021", "Logon", "NOTICE", "Failure", "Low", "Incorrect password", "Administrator authentication failed. Wrong password.", "Administrator authentication failed. Wrong password.",
    "51022", "Logon", "NOTICE", "Failure", "Low", "Other", "Administrator authentication failed. System Error", "Administrator authentication failed. System Error",
    "51106", "Logon", "NOTICE", "Failure", "Low", "Other", "Authentication for web services failed", "Authentication for web services failed.",
    "60075", "Logon", "NOTICE", "Success", "Informational", "", "Sponsor has successfully authenticated", "Sponsor has successfully authenticated",
    "60076", "Logon", "NOTICE", "Failure", "Low", "Other", "Sponsor authentication has failed", "Sponsor authentication has failed; please see Failure Code for more details",
    "60077", "Logon", "NOTICE", "Failure", "Low", "Other", "MyDevices user authentication has failed", "MyDevices user authentication has failed",
    "60078", "Logon", "INFO", "Success", "Informational", "", "MyDevices user has successfully authenticated", "MyDevices user has successfully authenticated",
    "60080", "Logon", "INFO", "Success", "Informational", "", "A SSH CLI user has successfully logged in", "A SSH CLI User has successfully logged in",
    "60081", "Logon", "INFO", "Failure", "Low", "No such user or password", "A SSH CLI user has attempted unsuccessfully to login", "A SSH CLI user has attempted unsuccessfully to login",
    "60082", "Logon", "INFO", "Failure", "Low", "User locked", "A SSH CLI user has attempted to login, however account is locked out", "A SSH CLI user has attempted to login, however account is locked out",
    "60135", "Logoff", "INFO", "Failure", "Low", "Other", "MyDevices user SSO logout has failed", "MyDevices user SSO logout has failed",
    "60136", "Logoff", "INFO", "Failure", "Low", "Other", "Sponsor user SSO logout has failed", "Sponsor user SSO logout has failed",
    "60204", "Logon", "INFO", "Success", "Informational", "", "System root CLI account has successfully logged in", "System root CLI account has successfully logged in",
    "60205", "Logon", "INFO", "Success", "Informational", "", "A CLI user has logged in from console", "A CLI user has logged in from console",
    "60206", "Logoff", "INFO", "Success", "Informational", "", "A CLI user has logged out from console", "A CLI user has logged out from console",
    "61012", "Logon", "INFO", "Success", "Informational", "", "ISE has authenticated against APIC successfully", "ISE has authenticated against APIC successfully",
    "61013", "Logon", "INFO", "Failure", "Low", "Other", "ISE failed to authenticate against APIC", "ISE failed to authenticate against APIC",
    "61014", "Logon", "INFO", "Success", "Informational", "", "ISE has refreshed authentication against APIC successfully", "ISE has refreshed authentication against APIC successfully",
    "61015", "Logon", "INFO", "Failure", "Low", "Other", "ISE failed to refresh authenticate against APIC", "ISE failed to refresh authenticate against APIC",
    "60507", "Logon", "ERROR", "Failure", "Low", "No such user", "ERS request rejected due to unauthorized user.", "ERS request was rejected because the user who sent the request is unauthorized.",
    "51025", "Logon", "NOTICE", "Failure", "Low", "Other", "Authentication for web services failed", "Authentication for web services failed.",
    "61076", "Logoff", "INFO", "Success", "Informational", "", "Sponsor has been successfully logged out", "Sponsor has been successfully logged out",
    "61077", "Logoff", "INFO", "Success", "Informational", "", "MyDevices has been successfully logged out", "MyDevices has been successfully logged out",
    "10003", "Logon", "ERROR", "Failure", "Low", "No such user", "Internal error: Administrator authentication received blank Administrator name", "Internal error: AAC RT component received Administrator authentication request",
    "10004", "Logon", "ERROR", "Failure", "Low", "Incorrect password", "Internal error: Administrator authentication received blank Administrator password", "Internal error: AAC RT component received an Administrator authentication request with blank admin password",
    "10005", "Logon", "INFO", "Success", "Informational", "", "Administrator authenticated successfully", "Administrator authenticated successfully",
    "10006", "Logon", "INFO", "Failure", "Low", "No such user or password", "Administrator authentication failed", "Administrator authentication failed",
    "10007", "Logon", "ERROR", "Failure", "Low", "Other", "Administrator authentication failed - DB Error", "Administrator authentication failed - DB Error",
    "22000", "Logon", "ERROR", "Failure", "Low", "Other", "Authentication resulted in internal error", "Authentication resulted in internal error",
    "22004", "Logon", "INFO", "Failure", "Low", "Incorrect password", "Wrong password", "Wrong password",
    "22028", "Logon", "INFO", "Failure", "Low", "No such user or password", "Authentication failed and the advanced options are ignored", "Authentication of the user failed and the advanced option settings specified in the identity portion of the relevant authentication policy were ignored. For PEAP, LEAP, EAP-FAST or RADIUS MSCHAP authentications, when authentication fails, ISE stops processing the request.",
    "22037", "Logon", "DEBUG", "Success", "Informational", "", "Authentication Passed", "Authentication Passed, Skipping Attribute Retrieval",
    "22040", "Logon", "INFO", "Failure", "Low", "Incorrect password", "Wrong password or invalid shared secret", "Wrong password or invalid shared secret",
    "22091", "Logon", "INFO", "Failure", "Low", "Logon violates policy", "Authentication failed. User account is disabled due to excessive failed authentication attempts at global level", "Authentication failed. User account is disabled due to excessive failed authentication attempts at global level.",
    "5400", "Logon", "NOTICE", "Failure", "Low", "Other", "Authentication failed", "User authentication failed. See FailureReason for more information",
    "5401", "Logon", "NOTICE", "Failure", "Low", "Other", "Authentication failed", "User authentication failed. See FailureReason for more information",
    "5412", "Logon", "NOTICE", "Failure", "Low", "Other", "TACACS+ authentication request ended with error", "TACACS+ authentication request ended with an error",
    "5418", "Logon", "NOTICE", "Failure", "Low", "Other", "Guest Authentication Failed", "Guest Authentication failed; please see Failure code for more details",
    "5447", "Logon", "NOTICE", "Success", "Informational", "", "MDM Authentication Passed", "MDM Authentication passed",
    "5448", "Logon", "NOTICE", "Failure", "Low", "Other", "MDM Authentication Failed", "MDM Authentication failed; please see Failure code for more details",
    "86010", "Logon", "INFO", "Failure", "Low", "No such user or password", "Guest user authentication failed", "Guest user authentication failed. Please check your password and account permission",
    "86011", "Logon", "INFO", "Failure", "Low", "User disabled", "Guest user is not enabled", "Guest user authentication failed. User is not enabled. Please contact your system administrator",
    "86014", "Logon", "INFO", "Failure", "Low", "User disabled", "User is suspended", "User authentication failed. User account is suspended",
    "86020", "Logon", "INFO", "Failure", "Low", "Other", "Guest Unknown Error", "User authentication failed. Please contact your System Administrator",
    "24015", "Logon", "DEBUG", "Success", "Informational", "", "Authenticating user against LDAP Server", "Authenticating user against LDAP Server",
    "24020", "Logon", "DEBUG", "Failure", "Low", "Incorrect password", "User authentication against the LDAP Server failed", "User authentication against the LDAP Server failed. The user entered the wrong password or the user record in the LDAP Server is disabled or expired",
    "24021", "Logon", "ERROR", "Failure", "Low", "Other", "User authentication ended with an error", "User authentication against LDAP Server ended with an error",
    "24022", "Logon", "DEBUG", "Success", "Informational", "", "User authentication succeeded", "User authentication against LDAP Server succeeded",
    "24050", "Logon", "WARN", "Failure", "Low", "Incorrect password", "Cannot authenticate with LDAP Identity Store because password was not present or was empty", "ISE did not receive user password or received empty password. Plain password authentication cannot be performed with no password or empty password",
    "24054", "Logon", "DEBUG", "Failure", "Low", "Password expired", "User authentication against LDAP server detected that user password has expired", "The password has expired but there are remaining grace authentications. The user needs to change it",
    "24055", "Logon", "DEBUG", "Failure", "Low", "Password expired", "User authentication against LDAP server detected that the user is authenticating for the first time after the password administrator set the password", "The user needs to change his password immediately",
    "24056", "Logon", "WARN", "Failure", "Low", "Password expired", "User authentication against LDAP server detected that user password has expired and there are no more grace authentications", "The user needs to contact the password administrator in order to have its password reset",
    "24057", "Logon", "WARN", "Failure", "Low", "Logon violates policy", "User authentication against LDAP server detected that the password failure limit has been reached and the account is locked", "The user needs to retry later or contact the password administrator to reset the password",
    "24337", "Logon", "DEBUG", "Success", "Informational", "", "Authentication Ticket (TGT) request succeeded", "Authentication Ticket (TGT) request succeeded",
    "24338", "Logon", "DEBUG", "Failure", "Low", "Other", "Authentication Ticket (TGT) request failed", "Authentication Ticket (TGT) request failed",
    "24402", "Logon", "INFO", "Success", "Informational", "", "User authentication against Active Directory succeeded", "User authentication against Active Directory succeeded",
    "24403", "Logon", "INFO", "Failure", "Low", "Other", "User authentication against Active Directory failed", "User authentication against Active Directory failed",
    "24406", "Logon", "DEBUG", "Failure", "Low", "No such user or password", "User authentication against Active Directory failed since user has invalid credentials", "User authentication against Active Directory failed since user has invalid credentials",
    "24407", "Logon", "DEBUG", "Failure", "Low", "Password expired", "User authentication against Active Directory failed since user is required to change his password", "User authentication against Active Directory failed since user is required to change his password",
    "24408", "Logon", "DEBUG", "Failure", "Low", "Incorrect password", "User authentication against Active Directory failed since user has entered the wrong password", "User authentication against Active Directory failed since user has entered the wrong password",
    "24409", "Logon", "DEBUG", "Failure", "Low", "User disabled", "User authentication against Active Directory failed since the user's account is disabled", "User authentication against Active Directory failed since the user's account is disabled",
    "24410", "Logon", "DEBUG", "Failure", "Low", "Logon violates policy", "User authentication against Active Directory failed since user is considered to be in restricted logon hours", "User authentication against Active Directory failed since user is considered to be in restricted logon hours",
    "24414", "Logon", "DEBUG", "Failure", "Low", "Account expired", "User authentication against Active Directory failed since the user's account has expired", "User authentication against Active Directory failed since the user's account has expired",
    "24415", "Logon", "DEBUG", "Failure", "Low", "User locked", "User authentication against Active Directory failed since user's account is locked out", "User authentication against Active Directory failed since user's account is locked out",
    "24418", "Logon", "ERROR", "Failure", "Low", "Logon violates policy", "Machine authentication against Active Directory failed since it is disabled in configuration", "Machine authentication against Active Directory failed since it is disabled in configuration",
    "24454", "Logon", "ERROR", "Failure", "Low", "Session expired", "User authentication against Active Directory failed because of a timeout error", "User authentication against Active Directory failed because of a timeout error",
    "24470", "Logon", "INFO", "Success", "Informational", "", "Machine authentication against Active Directory is successful", "Machine authentication against Active Directory is successful.",
    "24484", "Logon", "DEBUG", "Failure", "Low", "Password expired", "Machine authentication against Active Directory has failed because the machine's password has expired", "Machine authentication against Active Directory has failed because the machine's password has expired.",
    "24485", "Logon", "DEBUG", "Failure", "Low", "Incorrect password", "Machine authentication against Active Directory has failed because of wrong password", "Machine authentication against Active Directory has failed because of wrong password.",
    "24486", "Logon", "DEBUG", "Failure", "Low", "User disabled", "Machine authentication against Active Directory has failed because the machine's account is disabled", "Machine authentication against Active Directory has failed because the machine's account is disabled.",
    "24487", "Logon", "DEBUG", "Failure", "Low", "Logon violates policy", "Machine authentication against Active Directory failed since machine is considered to be in restricted logon hours", "Machine authentication against Active Directory failed since machine is considered to be in restricted logon hours",
    "24489", "Logon", "DEBUG", "Failure", "Low", "Account expired", "Machine authentication against Active Directory has failed because the machine's account has expired", "Machine authentication against Active Directory has failed because the machine's account has expired.",
    "24490", "Logon", "DEBUG", "Failure", "Low", "User locked", "Machine authentication against Active Directory has failed because the machine's account is locked out", "Machine authentication against Active Directory has failed because the machine's account is locked out.",
    "24491", "Logon", "DEBUG", "Failure", "Low", "No such user or password", "Machine authentication against Active Directory has failed because the machine has invalid credentials", "Machine authentication against Active Directory has failed because the machine has invalid credentials.",
    "24492", "Logon", "ERROR", "Failure", "Low", "No such user or password", "Machine authentication against Active Directory has failed", "Machine authentication against Active Directory has failed.",
    "24496", "Logon", "WARN", "Failure", "Low", "Logon violates policy", "Authentication rejected due to a white or black list restriction", "Authentication rejected due to a white or black list restriction",
    "24505", "Logon", "DEBUG", "Success", "Informational", "", "User authentication has succeeded", "User authentication against the RSA SecurID Server has succeeded.",
    "24508", "Logon", "DEBUG", "Failure", "Low", "Logon violates policy", "User authentication failed", "User authentication against RSA SecurID Server failed",
    "24518", "Logon", "DEBUG", "Failure", "Low", "Other", "User canceled New PIN operation; User authentication against RSA SecurIDServer failed", "User canceled New PIN operation; User authentication against RSA SecurID Server failed",
    "24547", "Logon", "WARN", "Failure", "Low", "Session expired", "RSA request timeout expired. RSA authentication session cancelled", "RSA request timeout expired. RSA authentication session cancelled.",
    "24612", "Logon", "INFO", "Success", "Informational", "", "Authentication against the RADIUS token server succeeded", "Authentication against the RADIUS token server succeeded.",
    "24613", "Logon", "ERROR", "Failure", "Low", "Other", "Authentication against the RADIUS token server failed", "Authentication against the RADIUS token server failed.",
    "24614", "Logon", "INFO", "Failure", "Low", "No such user", "RADIUS token server authentication failure is translated as Unknown user failure", "RADIUS token server authentication failure is translated as Unknown user failure.",
    "24639", "Logon", "DEBUG", "Success", "Informational", "", "Authentication passed via Passcode cache", "User record was found in Passcode cache, passcode matches the passcode on the authentication request. Authentication passed via Passcode cache.",
    "24704", "Logon", "DEBUG", "Failure", "Low", "Logon violates policy", "Authentication failed because identity credentials are ambiguous", "Authentication found several accounts matching to the given credentials (i.e identity name and password)",
    "24705", "Logon", "DEBUG", "Failure", "Low", "Other", "Authentication failed because ISE server is not joined to required domains", "Authentication failed because ISE server is not joined to required domains",
    "24706", "Logon", "DEBUG", "Failure", "Low", "Other", "Authentication failed because NTLM was blocked", "Authentication failed because NTLM was blocked",
    "24707", "Logon", "DEBUG", "Failure", "Low", "Other", "Authentication failed because all identity names have been rejected", "Authentication failed all identity names has been rejected according AD Identity Store Advanced Settings",
    "24708", "Logon", "DEBUG", "Failure", "Low", "No such user", "User not found in Active Directory. Some authentication domains were not available", "User not found in Active Directory. Some authentication domains were not available during identity resolution",
    "24709", "Logon", "DEBUG", "Failure", "Low", "No such user", "Host not found in Active Directory. Some authentication domains were not available", "Host not found in Active Directory. Some authentication domains were not available during identity resolution",
    "24712", "Logon", "DEBUG", "Failure", "Low", "Logon violates policy", "Authentication failed because domain trust is restricted", "Authentication failed because domain trust is restricted",
    "24814", "Logon", "INFO", "Failure", "Low", "Other", "The responding provider was unable to successfully authenticate the principal", "The responding provider was unable to successfully authenticate the principal",
    "24853", "Logon", "DEBUG", "Success", "Informational", "", "Plain text password authentication in external ODBC database succeeded", "Plain text password authentication in external ODBC database succeeded",
    "24854", "Logon", "DEBUG", "Failure", "Low", "No such user or password", "Plain text password authentication in external ODBC database failed", "Plain text password authentication in external ODBC database failed",
    "24860", "Logon", "DEBUG", "Failure", "Low", "No such user or password", "ODBC database indicated plain text password authentication failure", "ODBC database indicated plain text password authentication failure",
    "24890", "Logon", "WARN", "Failure", "Low", "Other", "Social Login operation failed", "Social Login operation failed. Check the message details for more information",
    "24716", "Logon", "INFO", "Success", "Informational", "", "Active Directory Kerberos ticket authentication succeeded", "Active Directory Kerberos ticket authentication succeeded",
    "24717", "Logon", "ERROR", "Failure", "Low", "Other", "Active Directory Kerberos ticket authentication failed", "Active Directory Kerberos ticket authentication failed",
    "24719", "Logon", "DEBUG", "Failure", "Low", "Incorrect password", "Active Directory Kerberos ticket authentication failed because of the ISE account password mismatch, integrity check failure or expired ticket", "Active Directory Kerberos ticket authentication failed because of the ISE account password mismatch, integrity check failure or expired ticket",
    "89157", "Logon", "ERROR", "Failure", "Low", "Other", "CMCS authentication failure", "ISE is unable to authenticate with the Cisco MDM Cloud Service",
    "89159", "Logon", "ERROR", "Failure", "Low", "Other", "APNS authentication failure", "ISE is unable to authenticate with the Apple Push Notification System (APNS)",
    "89160", "Logon", "INFO", "Success", "Informational", "", "MDM User Authentication completed", "The User Authentication part of mobile device enrollment has completed",
    "33102", "Logon", "INFO", "Success", "Informational", "", "Successful user login to ISE configuration mode", "ISE administrator logged in to ISE configuration mode",
    "33103", "Logon", "INFO", "Failure", "Low", "Other", "User login to ISE configuration mode failed", "Login to ISE configuration mode failed",
    "5200", "Logon", "NOTICE", "Success", "Informational", "", "Authentication succeeded", "User authentication ended successfully",
    "5201", "Logon", "NOTICE", "Success", "Informational", "", "Authentication succeeded", "User authentication ended successfully",
    "5231", "Logon", "NOTICE", "Success", "Informational", "", "Guest Authentication Passed", "Guest Authentication Passed",
    "11002", "Logon", "DEBUG", "Success", "Informational", "", "Returned RADIUS Access-Accept", "Returned RADIUS Access-Accept - authentication succeeded",
    "11003", "Logon", "DEBUG", "Failure", "Low", "Other", "Returned RADIUS Access-Reject", "Returned RADIUS Access-Reject - authentication failed",
    "11039", "Logon", "INFO", "Failure", "Low", "Other", "RADIUS authentication request rejected due to critical logging error", "A RADIUS authentication request was rejected due to a critical logging error.",
    "11052", "Logon", "ERROR", "Failure", "Low", "Other", "Authentication request dropped due to unsupported port number", "An authentication request was dropped because it was received through an unsupported port number.",
    "11812", "Logon", "INFO", "Success", "Informational", "", "EAP-MSCHAP authentication succeeded", "EAP-MSCHAP authentication succeeded.",
    "11813", "Logon", "INFO", "Failure", "Low", "Other", "EAP-MSCHAP authentication failed", "EAP-MSCHAP authentication failed.",
    "11814", "Logon", "INFO", "Success", "Informational", "", "Inner EAP-MSCHAP authentication succeeded", "EAP-MSCHAP authentication for the inner EAP method succeeded.",
    "11815", "Logon", "INFO", "Failure", "Low", "Other", "Inner EAP-MSCHAP authentication failed", "EAP-MSCHAP authentication for the inner EAP method failed.",
    "11823", "Logon", "INFO", "Failure", "Low", "Other", "EAP-MSCHAP authentication attempt failed", "EAP-MSCHAP authentication attempt failed.",
    "11824", "Logon", "DEBUG", "Success", "Informational", "", "EAP-MSCHAP authentication attempt passed", "EAP-MSCHAP authentication attempt passed.",
    "12005", "Logon", "INFO", "Success", "Informational", "", "EAP-MD5 authentication succeeded", "EAP-MD5 authentication succeeded.",
    "12006", "Logon", "INFO", "Failure", "Low", "Other", "EAP-MD5 authentication failed", "EAP-MD5 authentication failed.",
    "12208", "Logon", "INFO", "Failure", "Low", "Other", "Client certificate was received but authentication failed", "ISE received client certificate during tunnel establishment or inside the tunnel but the authentication failed.",
    "12306", "Logon", "INFO", "Success", "Informational", "", "PEAP authentication succeeded", "PEAP authentication succeeded.",
    "12307", "Logon", "INFO", "Failure", "Low", "Other", "PEAP authentication failed", "PEAP authentication failed.",
    "12308", "Logon", "WARN", "Failure", "Low", "Other", "Client sent Result TLV indicating failure", "Internal error, possibly in the supplicant: PEAP v0 authentication failed because client sent Result TLV indicating failure. Client indicates that it does not support Crypto-Binding TLV",
    "12506", "Logon", "INFO", "Success", "Informational", "", "EAP-TLS authentication succeeded", "EAP-TLS authentication succeeded.",
    "12507", "Logon", "INFO", "Failure", "Low", "Other", "EAP-TLS authentication failed", "EAP-TLS authentication failed.",
    "12528", "Logon", "INFO", "Success", "Informational", "", "Inner EAP-TLS authentication succeeded", "EAP-TLS authentication for the inner EAP method succeeded.",
    "12529", "Logon", "INFO", "Failure", "Low", "Other", "Inner EAP-TLS authentication failed", "EAP-TLS authentication for the inner EAP method failed.",
    "12612", "Logon", "INFO", "Success", "Informational", "", "EAP-GTC authentication succeeded", "EAP-GTC authentication has succeeded.",
    "12613", "Logon", "INFO", "Failure", "Low", "Other", "EAP-GTC authentication failed", "EAP-GTC authentication has failed.",
    "12614", "Logon", "INFO", "Success", "Informational", "", "Inner EAP-GTC authentication succeeded", "EAP-GTC authentication for the inner EAP method has succeeded.",
    "12615", "Logon", "INFO", "Failure", "Low", "Other", "Inner EAP-GTC authentication failed", "EAP-GTC authentication for the inner EAP method has failed.",
    "12623", "Logon", "INFO", "Failure", "Low", "Other", "EAP-GTC authentication attempt failed", "The EAP-GTC authentication attempt has failed.",
    "12624", "Logon", "DEBUG", "Success", "Informational", "", "EAP-GTC authentication attempt passed", "The EAP-GTC authentication attempt has passed.",
    "12705", "Logon", "INFO", "Success", "Informational", "", "LEAP authentication passed; Continuing protocol", "LEAP authentication passed. Continue LEAP protocol.",
    "12706", "Logon", "INFO", "Failure", "Low", "Other", "LEAP authentication failed; Finishing protocol", "LEAP authentication has failed. Protocol finished with a failure.",
    "12707", "Logon", "INFO", "Failure", "Low", "Other", "LEAP authentication error; Finishing protocol", "A LEAP authentication error has occurred. Protocol finished with an error.",
    "12854", "Logon", "WARN", "Failure", "Low", "Incorrect password", "Cannot authenticate because password was not present or was empty", "ISE did not receive user password or received empty password. Plain password authentication cannot be performed with no password or empty password",
    "12975", "Logon", "INFO", "Success", "Informational", "", "EAP-TTLS authentication succeeded", "EAP-TTLS authentication succeeded.",
    "12976", "Logon", "INFO", "Failure", "Low", "Other", "EAP-TTLS authentication failed", "EAP-TTLS authentication failed.",
    "11700", "Logon", "INFO", "Success", "Informational", "", "5G AKA Authentication succeeded", "5G AKA Authentication succeeded."
];
let EventOriginalTypeList = toscalar(EventFieldsLookup 
    | summarize make_set(EventOriginalType));
let CiscoISEAuthParser=(
    starttime: datetime=datetime(null), 
    endtime: datetime=datetime(null), 
    username_has_any: dynamic = dynamic([]),
    targetappname_has_any: dynamic = dynamic([]),
    srcipaddr_has_any_prefix: dynamic = dynamic([]),
    srchostname_has_any: dynamic = dynamic([]),
    eventtype_in: dynamic = dynamic([]),
    eventresultdetails_in: dynamic = dynamic([]),
    eventresult: string = '*',
    disabled: bool=false
    ) {
    Syslog
    | where not(disabled)
    // ************************** <Prefilterring> ******************************
    | where 
        (isnull(starttime) or TimeGenerated >= starttime)
        and (isnull(endtime) or TimeGenerated <= endtime)
        and ((array_length(username_has_any) == 0) or (SyslogMessage has_any (username_has_any)))
        and (array_length(targetappname_has_any) == 0) // TargetAppName not available in source
        and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(SyslogMessage, srcipaddr_has_any_prefix))
        and (array_length(srchostname_has_any) == 0) // SrcHostname not available in source
    // eventtype_in filtering done later in the parser
    // eventresultdetails_in filtering done later in the parser
    // eventresult filtering done later in the parser
    // ************************** </Prefilterring> *****************************
    | where ProcessName has_any ("CISE", "CSCO")
    | parse kind = regex SyslogMessage with @"\d{10}\s" EventOriginalType @"\s(NOTICE|INFO|WARN|WARNING|ERROR|FATAL|DEBUG)"
    | where EventOriginalType in (EventOriginalTypeList)
    | lookup EventFieldsLookup on EventOriginalType
    // Filtering on eventtype_in, eventresultdetails_in and eventresult
    | where ((array_length(eventtype_in) == 0) or (EventType in~ (eventtype_in)))
        and ((array_length(eventresultdetails_in) == 0) or (EventResultDetails in~ (eventresultdetails_in)))
        and ((eventresult == "*") or (EventResult == eventresult))
    | parse-kv SyslogMessage as (FailureReason: string, NetworkDeviceName: string, Protocol: string, DestinationIPAddress: string, DestinationPort: int, ['User-Name']: string, UserName: string, User: string, ['Remote-Address']: string, ['Device IP Address']: string, ['Device Port']: int, ['cisco-av-pair=audit-session-id']: string, ['Caller-Station-ID']: string) with (pair_delimiter=',', kv_delimiter='=')
    | project-rename
        LogonProtocol=Protocol
        ,
        TargetIpAddr=DestinationIPAddress
        ,
        TargetPortNumber=DestinationPort
        ,
        TargetSessionId=["cisco-av-pair=audit-session-id"]
        ,
        SrcPortNumber=['Device Port']
    | invoke _ASIM_ResolveSrcFQDN("['Caller-Station-ID']")
    | extend
        EventStartTime = coalesce(EventTime, TimeGenerated)
        ,
        EventEndTime = coalesce(EventTime, TimeGenerated)
    | extend DvcHostname = coalesce(NetworkDeviceName, Computer, HostName)
    | extend TargetUsername = coalesce(['User-Name'], UserName, User)
    | extend
        TargetUsernameType = _ASIM_GetUsernameType(TargetUsername)
        ,
        SrcIpAddr = coalesce(['Device IP Address'], ['Remote-Address'], tostring(extract(@"Caller-Station-ID=(\d{1,3}\.\d{1.3}\.\d{1,3}\.\d{1,3})", 1, SyslogMessage)), "")
    | extend EventOriginalResultDetails = case(isnotempty(FailureReason), FailureReason, EventOriginalResultDetails)
    | extend DvcIpAddr = iif(isnotempty(HostIP) and HostIP != "Unknown IP", HostIP, extract(@"(\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})", 1, Computer))
    // ********************** <Postfilterring> **********************************
    | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
        and ((array_length(srcipaddr_has_any_prefix) == 0) or has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix))
    // ********************** </Postfilterring> *********************************
    // mapping ASimMatchingUsername
    | extend temp_isMatchTargetUsername=TargetUsername has_any(username_has_any)
    // ActorUsername not coming from source. Hence, not mapped.
    | extend ASimMatchingUsername = case
        (
                                    array_length(username_has_any) == 0,
                                    "-",
                                    temp_isMatchTargetUsername,
                                    "TargetUsername",
                                    "No match"
                                )
    | extend 
        EventVendor = "Cisco"
        ,
        EventProduct = "ISE"
        ,
        EventProductVersion = "3.2"
        ,
        EventCount = int(1)
        ,
        EventSchema = "Authentication"
        ,
        EventSchemaVersion = "0.1.3"     
    // ************************* <Aliases> **********************
    | extend 
        Dvc = coalesce(DvcIpAddr, DvcHostname)
        ,
        IpAddr = SrcIpAddr
        ,
        Dst = TargetIpAddr
        ,
        Src = SrcIpAddr
        ,
        User = TargetUsername
    // ************************* </Aliases> ******************** 
    | project-away
        TenantId,
        SourceSystem,
        MG,
        Computer,
        EventTime,
        Facility,
        HostName,
        SeverityLevel,
        SyslogMessage,
        HostIP,
        ProcessName,
        ProcessID,
        _ResourceId,
        FailureReason,
        NetworkDeviceName,
        ['User-Name'],
        UserName,
        User,
        ['Remote-Address'],
        ['Device IP Address'],
        ['Caller-Station-ID']
};
CiscoISEAuthParser(
    starttime=starttime,
    endtime=endtime,
    username_has_any=username_has_any,
    targetappname_has_any=targetappname_has_any,
    srcipaddr_has_any_prefix=srcipaddr_has_any_prefix,
    srchostname_has_any=srchostname_has_any,
    eventtype_in=eventtype_in,
    eventresultdetails_in=eventresultdetails_in,
    eventresult=eventresult,
    disabled=disabled
)
}



//
// Function Name: vimAuthenticationCiscoMeraki
// Description: This ASIM parser supports normalizing Cisco Meraki logs ingested in 'meraki_CL' table to the ASIM Authentication normalized schema. Cisco Meraki events are generated from network activity and security events from Meraki devices such as firewalls, switches, and access points. These logs are captured through the Cisco Meraki Sentinel connector which uses a Linux agent to collect logs in Syslog format.

// Version: 
// Last Updated: 
//
.create-or-alter function with (skipvalidation=true) vimAuthenticationCiscoMeraki(    ['starttime']:datetime=datetime(null),
    ['endtime']:datetime=datetime(null),
    ['username_has_any']:dynamic=dynamic([]),
    ['targetappname_has_any']:dynamic=dynamic([]),
    ['srcipaddr_has_any_prefix']:dynamic=dynamic([]),
    ['srchostname_has_any']:dynamic=dynamic([]),
    ['eventtype_in']:dynamic=dynamic([]),
    ['eventresultdetails_in']:dynamic=dynamic([]),
    ['eventresult']:string='*',
    ['disabled']:bool=False)
{
let LogSubTypeList = dynamic(["8021x_auth", "wpa_auth", "splash_auth", "8021x_deauth", "8021x_client_deauth", "wpa_deauth", "8021x_eap_failure", "8021x_eap_success"]);
let EventResultDetailsLookup = datatable (reason: string, EventResultDetails: string)
    [
    "0", "Other",
    "1", "Other",
    "2", "Password expired",
    "3", "Other",
    "4", "Session expired",
    "5", "Other",
    "6", "Other",
    "7", "Other",
    "8", "Other",
    "9", "Other",
    "10", "Logon violates policy",
    "11", "Logon violates policy",
    "12", "Other",
    "13", "Logon violates policy",
    "14", "Other",
    "15", "Other",
    "16", "Other",
    "17", "Other",
    "18", "Incorrect key",
    "19", "Incorrect key",
    "20", "Incorrect key",
    "21", "Other",
    "22", "Other",
    "23", "Other",
    "24", "Logon violates policy",
];
let EventFieldsLookup = datatable (
    LogSubType: string,
    EventResult: string,
    EventType: string,
    EventSeverity: string
)
    [
    "8021x_auth", "Success", "Logon", "Informational",
    "wpa_auth", "Success", "Logon", "Informational",
    "splash_auth", "Success", "Logon", "Informational",
    "8021x_eap_success", "Success", "Logon", "Informational",
    "8021x_deauth", "Success", "Logoff", "Informational",
    "8021x_client_deauth", "Success", "Logoff", "Informational",
    "wpa_deauth", "Success", "Logoff", "Informational",
    "8021x_eap_failure", "Failure", "Logon", "Low",
    "disassociation", "Failure", "Logon", "Low",
];
let parser = (
    starttime: datetime=datetime(null), 
    endtime: datetime=datetime(null), 
    username_has_any: dynamic = dynamic([]),
    targetappname_has_any: dynamic = dynamic([]),
    srcipaddr_has_any_prefix: dynamic = dynamic([]),
    srchostname_has_any: dynamic = dynamic([]),
    eventtype_in: dynamic = dynamic([]),
    eventresultdetails_in: dynamic = dynamic([]),
    eventresult: string = '*',
    disabled: bool=false
    ) {
    (
        meraki_CL
        | project-rename LogMessage =  Message
        )
    | where not(disabled)
        and LogMessage has "events"
        and (LogMessage has_any (LogSubTypeList) or LogMessage has_all ("disassociation", "auth_neg_failed"))
        and (isnull(starttime) or TimeGenerated >= starttime)
        and (isnull(endtime) or TimeGenerated <= endtime)
        and ((array_length(username_has_any) == 0) or LogMessage has_any (username_has_any))
        and (array_length(targetappname_has_any) == 0) // TargetAppName not available in source
        and ((array_length(srcipaddr_has_any_prefix) == 0) or (has_any_ipv4_prefix(LogMessage, srcipaddr_has_any_prefix)))
        and (array_length(srchostname_has_any) == 0) // SrcHostname not available in source
    // eventtype_in filtering done later in the parser
    // eventresultdetails_in filtering done later in the parser
    // eventresult filtering done later in the parser
    | extend Parser = extract_all(@"(\d+.\d+)\s([\w\-\_]+)\s([\w\-\_]+)\s([\S\s]+)$", dynamic([1, 2, 3, 4]), LogMessage)[0]
    | extend
        Epoch = tostring(Parser[0]),
        Device = tostring(Parser[1]),
        LogType = tostring(Parser[2]),
        Substring = tostring(Parser[3])
    | extend EpochTimestamp = split(Epoch, ".")
    | extend EventStartTime = unixtime_seconds_todatetime(tolong(EpochTimestamp[0]))
    | extend EventEndTime = EventStartTime
    | where LogType == "events"
    | parse Substring with * "type=" LogSubType: string " " restOfMessage: string
    | where LogSubType in (LogSubTypeList) or (LogSubType == "disassociation" and Substring has "auth_neg_failed")
    | invoke _ASIM_ResolveDvcFQDN('Device')
    | parse-kv Substring as(last_known_client_ip: string, ip: string, client_ip: string, client_mac: string, identity: string, reason: string, aid: string) with (pair_delimiter=" ", kv_delimiter="=", quote="'")
    | extend TargetUsername = identity
    | extend TargetUsername = trim('"', TargetUsername)
    // post-filtering username_has_any
    | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
    // mapping ASimMatchingUsername
    | extend temp_isMatchTargetUsername=TargetUsername has_any(username_has_any)
    // ActorUsername not coming from source. Hence, not mapped.
    | extend ASimMatchingUsername = case
        (
                                    array_length(username_has_any) == 0,
                                    "-",
                                    temp_isMatchTargetUsername,
                                    "TargetUsername",
                                    "No match"
                                )
    | extend
        Dvc = DvcHostname, 
        aid = trim('"', aid)
    | extend
        SrcIpAddr = tostring(split(coalesce(last_known_client_ip, ip, client_ip), " ")[0]),
        DvcMacAddr = client_mac,
        AdditionalFields = bag_pack("aid", aid),
        EventOriginalType = LogType,
        EventOriginalSubType = LogSubType,
        TargetUsernameType = iff(isnotempty(TargetUsername), "Simple", ""),
        EventUid = _ResourceId
    | extend
        SrcIpAddr = trim('"', SrcIpAddr),
        DvcMacAddr = trim('"', DvcMacAddr),
        reason = trim('"', reason)
    // post-filtering srcipaddr_has_any_prefix
    | where ((array_length(srcipaddr_has_any_prefix) == 0) or (has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix)))
    | extend
        DvcIpAddr = SrcIpAddr,
        IpAddr = SrcIpAddr,
        User = TargetUsername
    | lookup EventFieldsLookup on LogSubType
    | lookup EventResultDetailsLookup on reason
    | extend EventResultDetails = iff(tolong(reason) between (25 .. 65535), "Other", EventResultDetails)
    // Filtering on eventtype_in, eventresultdetails_in and eventresult
    | where ((array_length(eventtype_in) == 0) or EventType in~ (eventtype_in))
        and (array_length(eventresultdetails_in) == 0 or EventResultDetails in~ (eventresultdetails_in))
        and (eventresult == "*" or (EventResult == eventresult))
    | extend
        EventCount=int(1),
        EventProduct="Meraki",
        EventVendor="Cisco",
        EventSchema="Authentication",
        EventSchemaVersion="0.1.3"
    | project-away
        LogMessage,
        Parser,
        Epoch,
        EpochTimestamp,
        Device,
        Substring,
        LogType,
        LogSubType,
        restOfMessage,
        reason,
        last_known_client_ip,
        client_ip,
        ip,
        client_mac,
        identity,
        aid,
        TenantId,
        SourceSystem,
        Computer,
        _ResourceId,
        MG
};
parser(
    starttime=starttime,
    endtime=endtime,
    username_has_any=username_has_any,
    targetappname_has_any=targetappname_has_any,
    srcipaddr_has_any_prefix=srcipaddr_has_any_prefix,
    srchostname_has_any=srchostname_has_any,
    eventtype_in=eventtype_in,
    eventresultdetails_in=eventresultdetails_in,
    eventresult=eventresult,
    disabled=disabled
)
}



//
// Function Name: vimAuthenticationCiscoMerakiSyslog
// Description: This ASIM parser supports normalizing Cisco Meraki logs ingested in 'Syslog' table to the ASIM Authentication normalized schema. Cisco Meraki events are generated from network activity and security events from Meraki devices such as firewalls, switches, and access points. These logs are captured through the Cisco Meraki Sentinel connector which uses a Linux agent to collect logs in Syslog format.

// Version: 
// Last Updated: 
//
.create-or-alter function with (skipvalidation=true) vimAuthenticationCiscoMerakiSyslog(    ['starttime']:datetime=datetime(null),
    ['endtime']:datetime=datetime(null),
    ['username_has_any']:dynamic=dynamic([]),
    ['targetappname_has_any']:dynamic=dynamic([]),
    ['srcipaddr_has_any_prefix']:dynamic=dynamic([]),
    ['srchostname_has_any']:dynamic=dynamic([]),
    ['eventtype_in']:dynamic=dynamic([]),
    ['eventresultdetails_in']:dynamic=dynamic([]),
    ['eventresult']:string='*',
    ['disabled']:bool=False)
{
let LogSubTypeList = dynamic(["8021x_auth", "wpa_auth", "splash_auth", "8021x_deauth", "8021x_client_deauth", "wpa_deauth", "8021x_eap_failure", "8021x_eap_success"]);
let EventResultDetailsLookup = datatable (reason: string, EventResultDetails: string)
    [
    "0", "Other",
    "1", "Other",
    "2", "Password expired",
    "3", "Other",
    "4", "Session expired",
    "5", "Other",
    "6", "Other",
    "7", "Other",
    "8", "Other",
    "9", "Other",
    "10", "Logon violates policy",
    "11", "Logon violates policy",
    "12", "Other",
    "13", "Logon violates policy",
    "14", "Other",
    "15", "Other",
    "16", "Other",
    "17", "Other",
    "18", "Incorrect key",
    "19", "Incorrect key",
    "20", "Incorrect key",
    "21", "Other",
    "22", "Other",
    "23", "Other",
    "24", "Logon violates policy",
];
let EventFieldsLookup = datatable (
    LogSubType: string,
    EventResult: string,
    EventType: string,
    EventSeverity: string
)
    [
    "8021x_auth", "Success", "Logon", "Informational",
    "wpa_auth", "Success", "Logon", "Informational",
    "splash_auth", "Success", "Logon", "Informational",
    "8021x_eap_success", "Success", "Logon", "Informational",
    "8021x_deauth", "Success", "Logoff", "Informational",
    "8021x_client_deauth", "Success", "Logoff", "Informational",
    "wpa_deauth", "Success", "Logoff", "Informational",
    "8021x_eap_failure", "Failure", "Logon", "Low",
    "disassociation", "Failure", "Logon", "Low",
];
let parser = (
    starttime: datetime=datetime(null), 
    endtime: datetime=datetime(null), 
    username_has_any: dynamic = dynamic([]),
    targetappname_has_any: dynamic = dynamic([]),
    srcipaddr_has_any_prefix: dynamic = dynamic([]),
    srchostname_has_any: dynamic = dynamic([]),
    eventtype_in: dynamic = dynamic([]),
    eventresultdetails_in: dynamic = dynamic([]),
    eventresult: string = '*',
    disabled: bool=false
    ) {
    (
        Syslog
        | where Computer in (_ASIM_GetSourceBySourceType('CiscoMeraki'))
        | project-rename LogMessage =  SyslogMessage
        )
    | where not(disabled)
        and LogMessage has "events"
        and (LogMessage has_any (LogSubTypeList) or LogMessage has_all ("disassociation", "auth_neg_failed"))
        and (isnull(starttime) or TimeGenerated >= starttime)
        and (isnull(endtime) or TimeGenerated <= endtime)
        and ((array_length(username_has_any) == 0) or LogMessage has_any (username_has_any))
        and (array_length(targetappname_has_any) == 0) // TargetAppName not available in source
        and ((array_length(srcipaddr_has_any_prefix) == 0) or (has_any_ipv4_prefix(LogMessage, srcipaddr_has_any_prefix)))
        and (array_length(srchostname_has_any) == 0) // SrcHostname not available in source
    // eventtype_in filtering done later in the parser
    // eventresultdetails_in filtering done later in the parser
    // eventresult filtering done later in the parser
    | extend Parser = extract_all(@"(\d+.\d+)\s([\w\-\_]+)\s([\w\-\_]+)\s([\S\s]+)$", dynamic([1, 2, 3, 4]), LogMessage)[0]
    | extend
        Epoch = tostring(Parser[0]),
        Device = tostring(Parser[1]),
        LogType = tostring(Parser[2]),
        Substring = tostring(Parser[3])
    | extend EpochTimestamp = split(Epoch, ".")
    | extend EventStartTime = unixtime_seconds_todatetime(tolong(EpochTimestamp[0]))
    | extend EventEndTime = EventStartTime
    | where LogType == "events"
    | parse Substring with * "type=" LogSubType: string " " restOfMessage: string
    | where LogSubType in (LogSubTypeList) or (LogSubType == "disassociation" and Substring has "auth_neg_failed")
    | invoke _ASIM_ResolveDvcFQDN('Device')
    | parse-kv Substring as(last_known_client_ip: string, ip: string, client_ip: string, client_mac: string, identity: string, reason: string, aid: string) with (pair_delimiter=" ", kv_delimiter="=", quote="'")
    | extend TargetUsername = identity
    | extend TargetUsername = trim('"', TargetUsername)
    // post-filtering username_has_any
    | where ((array_length(username_has_any) == 0) or (TargetUsername has_any (username_has_any)))
    // mapping ASimMatchingUsername
    | extend temp_isMatchTargetUsername=TargetUsername has_any(username_has_any)
    // ActorUsername not coming from source. Hence, not mapped.
    | extend ASimMatchingUsername = case
        (
                                    array_length(username_has_any) == 0,
                                    "-",
                                    temp_isMatchTargetUsername,
                                    "TargetUsername",
                                    "No match"
                                )
    | extend
        Dvc = DvcHostname, 
        aid = trim('"', aid)
    | extend
        SrcIpAddr = tostring(split(coalesce(last_known_client_ip, ip, client_ip), " ")[0]),
        DvcMacAddr = client_mac,
        AdditionalFields = bag_pack("aid", aid),
        EventOriginalType = LogType,
        EventOriginalSubType = LogSubType,
        TargetUsernameType = iff(isnotempty(TargetUsername), "Simple", ""),
        EventUid = _ResourceId
    | extend
        SrcIpAddr = trim('"', SrcIpAddr),
        DvcMacAddr = trim('"', DvcMacAddr),
        reason = trim('"', reason)
    // post-filtering srcipaddr_has_any_prefix
    | where ((array_length(srcipaddr_has_any_prefix) == 0) or (has_any_ipv4_prefix(SrcIpAddr, srcipaddr_has_any_prefix)))
    | extend
        DvcIpAddr = SrcIpAddr,
        IpAddr = SrcIpAddr,
        User = TargetUsername
    | lookup EventFieldsLookup on LogSubType
    | lookup EventResultDetailsLookup on reason
    | extend EventResultDetails = iff(tolong(reason) between (25 .. 65535), "Other", EventResultDetails)
    // Filtering on eventtype_in, eventresultdetails_in and eventresult
    | where ((array_length(eventtype_in) == 0) or EventType in~ (eventtype_in))
        and (array_length(eventresultdetails_in) == 0 or EventResultDetails in~ (eventresultdetails_in))
        and (eventresult == "*" or (EventResult == eventresult))
    | extend
        EventCount=int(1),
        EventProduct="Meraki",
        EventVendor="Cisco",
        EventSchema="Authentication",
        EventSchemaVersion="0.1.3"
    | project-away
        LogMessage,
        Parser,
        Epoch,
        EpochTimestamp,
        Device,
        Substring,
        LogType,
        LogSubType,
        restOfMessage,
        reason,
        last_known_client_ip,
        client_ip,
        ip,
        client_mac,
        identity,
        aid,
        TenantId,
        SourceSystem,
        Computer,
        _ResourceId,
        MG,
        EventTime,
        Facility,
        HostName,
        SeverityLevel,
        ProcessID,
        HostIP,
        ProcessName,ASimMatchingUsername,CollectorHostName,temp_isMatchTargetUsername
};
parser(
    starttime=starttime,
    endtime=endtime,
    username_has_any=username_has_any,
    targetappname_has_any=targetappname_has_any,
    srcipaddr_has_any_prefix=srcipaddr_has_any_prefix,
    srchostname_has_any=srchostname_has_any,
    eventtype_in=eventtype_in,
    eventresultdetails_in=eventresultdetails_in,
    eventresult=eventresult,
    disabled=disabled
)
}



//
// Function Name: vimAuthenticationCrowdStrikeFalconHost
// Description: This ASIM parser supports normalizing CrowdStrike Falcon Endpoint Protection logs to the ASIM Authentication normalized schema. These events are captured through CrowdStrike Falcon Endpoint Protection data connector which allows you to easily connect your CrowdStrike Falcon Event Stream with Microsoft Sentinel.

// Version: 
// Last Updated: 
//
.create-or-alter function with (skipvalidation=true) vimAuthenticationCrowdStrikeFalconHost(    ['starttime']:datetime=datetime(null),
    ['endtime']:datetime=datetime(null),
    ['username_has_any']:dynamic=dynamic([]),
    ['targetappname_has_any']:dynamic=dynamic([]),
    ['srcipaddr_has_any_prefix']:dynamic=dynamic([]),
    ['srchostname_has_any']:dynamic=dynamic([]),
    ['eventtype_in']:dynamic=dynamic([]),
    ['eventresultdetails_in']:dynamic=dynamic([]),
    ['eventresult']:string='*',
    ['disabled']:bool=False)
{
let EventSeverityLookup = datatable (LogSeverity: string, EventSeverity: string)
  [
  "0", "Informational",
  "1", "Informational",
  "2", "Low",
  "3", "Medium",
  "4", "High",
  "5", "High"
];
let parser = ( 
    starttime: datetime=datetime(null), 
    endtime: datetime=datetime(null), 
    username_has_any: dynamic = dynamic([]),
    targetappname_has_any: dynamic = dynamic([]),
    srcipaddr_has_any_prefix: dynamic = dynamic([]),
    srchostname_has_any: dynamic = dynamic([]),
    eventtype_in: dynamic = dynamic([]),
    eventresultdetails_in: dynamic = dynamic([]),
    eventresult: string = '*',
    disabled: bool=false
    ) {
    CommonSecurityLog
    | where not(disabled)
    | where (isnull(starttime) or TimeGenerated >= starttime)
        and (isnull(endtime) or TimeGenerated <= endtime)
        and (DeviceVendor == "CrowdStrike" and DeviceProduct == "FalconHost")
        and (DeviceEventCategory == "AuthActivityAuditEvent" and DeviceEventClassID in ("userAuthenticate", "twoFactorAuthenticate"))
        and ((array_length(username_has_any) == 0) or DestinationUserName has_any (username_has_any))
        and (array_length(targetappname_has_any) == 0 or ProcessName has_any (targetappname_has_any))
        and (array_length(srcipaddr_has_any_prefix) == 0) // SrcIpAddr not available in source
        and (array_length(srchostname_has_any) == 0) // SrcHostname not available in source
        // eventtype_in filtering done later in the parser
        and array_length(eventresultdetails_in) == 0 // EventResultDetails not available in source
    // eventresult filtering done later in the parser
    | extend
        EventResult = iff(EventOutcome == "true", "Success", "Failure"),
        EventType = "Logon"
    | where (eventresult == '*' or eventresult =~ EventResult)
        and (array_length(eventtype_in) == 0 or EventType has_any (eventtype_in))
    | lookup EventSeverityLookup on LogSeverity
    | extend
        EventStartTime = todatetime(DeviceCustomDate1),
        EventCount = int(1),
        EventSchema = "Authentication",
        EventSchemaVersion = "0.1.3",
        EventProduct = "FalconHost",
        EventVendor = "CrowdStrike"
    | project-rename 
        TargetIpAddr = DestinationTranslatedAddress,
        EventUid = _ItemId,
        EventOriginalSeverity = LogSeverity,
        EventOriginalSubType = DeviceEventClassID,
        EventOriginalType = DeviceEventCategory,
        EventProductVersion = DeviceVersion,
        EventOriginalResultDetails = EventOutcome,
        TargetUsername = DestinationUserName,
        TargetAppName = ProcessName
    // mapping ASimMatchingUsername
    | extend temp_isMatchTargetUsername=TargetUsername has_any(username_has_any)
    // ActorUsername not coming from source. Hence, not mapped.
    | extend ASimMatchingUsername = case
        (
                                    array_length(username_has_any) == 0,
                                    "-",
                                    temp_isMatchTargetUsername,
                                    "TargetUsername",
                                    "No match"
                                )
    | extend
        EventEndTime = EventStartTime,
        DvcIpAddr = TargetIpAddr,
        TargetUsernameType = _ASIM_GetUsernameType(TargetUsername),
        TargetUserType = _ASIM_GetUserType(TargetUsername, ""),
        TargetAppType = iff(isnotempty(TargetAppName), "Service", ""),
        LogonMethod = iff(EventOriginalSubType =~ "userAuthenticate", "Username and Password", "Two Factor Authentication")
    | extend
        User = TargetUsername,
        Dst = TargetIpAddr,
        Dvc = coalesce(DvcIpAddr, EventProduct),
        Application = TargetAppName
    | project-away 
        Source*,
        Destination*,
        Device*,
        AdditionalExtensions,
        CommunicationDirection,
        Computer,
        EndTime,
        FieldDevice*,
        Flex*,
        File*,
        Old*,
        MaliciousIP*,
        OriginalLogSeverity,
        Process*,
        Protocol,
        Activity,
        ReceivedBytes,
        SentBytes,
        Remote*,
        Request*,
        SimplifiedDeviceAction,
        StartTime,
        TenantId,
        Threat*,
        IndicatorThreatType,
        ExternalID,
        ReportReferenceLink,
        ReceiptTime,
        Reason,
        ApplicationProtocol,
        _ResourceId,
        ExtID,
        Message,
        temp_*
};
parser(
    starttime=datetime(null),
    endtime=datetime(null),
    username_has_any=dynamic([]),
    targetappname_has_any=dynamic([]),
    srcipaddr_has_any_prefix=dynamic([]),
    srchostname_has_any=dynamic([]),
    eventtype_in=dynamic([]),
    eventresultdetails_in=dynamic([]),
    eventresult=dynamic([]),
    disabled=false
)
}



//
// Function Name: vimAuthenticationEmpty
// Description: This function returns an empty ASIM Authentication schema.

// Version: 
// Last Updated: 
//
.create-or-alter function with (skipvalidation=true) vimAuthenticationEmpty
{
let EmptyAuthenticationTable=datatable(
    EventProduct:string
  , EventProductVersion: string
  , EventVendor:string
  , EventCount:int
  , EventReportUrl:string
  , EventSchemaVersion:string
  , EventSchema:string
  , TimeGenerated:datetime
  , EventOriginalUid:string
  , EventOriginalType:string
  , EventOriginalSubType:string
  , EventMessage:string
  , EventResult:string
  , EventResultDetails:string
  , EventOriginalResultDetails:string
  , EventStartTime:datetime
  , EventEndTime:datetime
  , EventType:string
  , EventSubType:string
  , EventUid:string
  , EventSeverity:string
  , EventOriginalSeverity:string
  , EventOwner:string
  , ActorSessionId:string
  , TargetSessionId:string
  , ActorUserId:string
  , ActorUsername:string
  , ActorUserType:string
  , ActorUserIdType:string
  , ActorUsernameType:string
  , ActorScopeId:string
  , ActorOriginalUserType:string
  , TargetUserId:string
  , TargetUsername:string
  , TargetUserType:string
  , SrcDvcId:string
  , SrcDvcIdType:string
  , SrcDeviceType:string
  , SrcDvcOs:string
  , HttpUserAgent:string
  , SrcIsp:string
  , SrcGeoCity:string
  , SrcGeoCountry:string
  , SrcGeoRegion:string
  , SrcGeoLatitude:real
  , SrcGeoLongitude:real
  , SrcIpAddr:string
  , SrcPortNumber:string
  , SrcHostname:string
  , SrcDomain:string
  , SrcDomainType:string
  , SrcFQDN:string
  , SrcDescription:string
  , SrcDvcScopeId:string
  , SrcRiskLevel:int
  , SrcOriginalRiskLevel:string
  , ActingAppId:string
  , ActingAppName:string
  , ActingAppType:string
  , ActingOriginalAppType:string
  , TargetAppId:string
  , TargetAppName:string
  , TargetAppType:string
  , TargetOriginalAppType:string
  , TargetDvcId:string
  , TargetDvcIdType:string
  , TargetHostname:string
  , TargetDomain:string
  , TargetDomainType:string
  , TargetFQDN:string
  , TargetDescription:string
  , TargetDeviceType:string
  , TargetIpAddr:string
  , TargetDvcOs:string
  , TargetUrl:string
  , TargetPortNumber:int
  , TargetDvcScope:string
  , TargetDvcScopeId:string
  , TargetGeoCity:string
  , TargetGeoCountry:string
  , TargetGeoRegion:string
  , TargetGeoLatitude:real
  , TargetGeoLongitude:real
  , LogonMethod: string	
  , LogonProtocol: string	
  , TargetUserIdType: string	
  , TargetUsernameType: string	
  , UserScope:string
  , UserScopeId:string
  , TargetOriginalUserType:string
  , TargetUserSessionId:string
  , User: string	
  , IpAddr: string
  , SrcDvcHostnameType: string	
  , LogonTarget: string
  , Dvc: string	
  , DvcId: string
  , DvcIpAddr: string	
  , DvcHostname: string
  , DvcDomain:string
  , DvcDomainType:string
  , DvcFQDN:string
  , DvcDescription:string
  , DvcIdType:string
  , DvcMacAddr:string
  , DvcZone:string
  , DvcOs:string
  , DvcOsVersion:string
  , DvcAction:string
  , DvcOriginalAction:string
  , DvcScope:string
  , DvcScopeOd:string
  , AdditionalFields:dynamic
  , Type:string
  , Src:string
  , Dst:string
  , Rule:string
  , RuleName:string
  , RuleNumber:int
  , ThreatId:string
  , ThreatName:string
  , ThreatCategory:string
  , ThreatOriginalRiskLevel:string
  , ThreatOriginalConfidence:string
  , ThreatIsActive:bool
  , ThreatField:string
  , ThreatConfidence:int
  , ThreatRiskLevel:string
  , ThreatFirstReportedTime:datetime
  , ThreatLastReportedTime:datetime
  , Application:string
  )[];
EmptyAuthenticationTable
}



//
// Function Name: vimAuthenticationGoogleWorkspace
// Description: This ASIM parser supports normalizing the Google Workspace sign-in logs(type=login) ingested in 'GWorkspace_ReportsAPI_login_CL' table to the ASIM Authentication normalized schema.

// Version: 
// Last Updated: 
//
.create-or-alter function with (skipvalidation=true) vimAuthenticationGoogleWorkspace(    ['starttime']:datetime=datetime(null),
    ['endtime']:datetime=datetime(null),
    ['username_has_any']:dynamic=dynamic([]),
    ['targetappname_has_any']:dynamic=dynamic([]),
    ['srcipaddr_has_any_prefix']:dynamic=dynamic([]),
    ['srchostname_has_any']:dynamic=dynamic([]),
    ['eventtype_in']:dynamic=dynamic([]),
    ['eventresultdetails_in']:dynamic=dynamic([]),
    ['eventresult']:string='*',
    ['disabled']:bool=False)
{
let parser = (
  starttime: datetime=datetime(null), 
  endtime: datetime=datetime(null), 
  username_has_any: dynamic = dynamic([]),
  targetappname_has_any: dynamic = dynamic([]),
  srcipaddr_has_any_prefix: dynamic = dynamic([]),
  srchostname_has_any: dynamic = dynamic([]),
  eventtype_in: dynamic = dynamic([]),
  eventresultdetails_in: dynamic = dynamic([]),
  eventresult: string = '*',
  disabled: bool=false
  )
{
    let GoogleWorkspaceSchema = datatable
(
    event_name_s: string,
    event_type_s: string,
    id_uniqueQualifier_s: string,
    actor_email_s: string,
    actor_profileId_s: string,
    IPAddress: string,
    login_challenge_method_s: string,
    id_applicationName_s: string,
    affected_email_address_s: string,
    is_suspicious_b: bool,
    is_second_factor_b: bool,
    login_type_s: string,
    sensitive_action_name_s: string,
    login_challenge_status_s: string,
    TimeGenerated: datetime,
    _ItemId: string,
    _ResourceId: string,
    Computer: string,
    MG: string,
    ManagementGroupName: string,
    RawData: string,
    SourceSystem: string,
    TenantId: string
)[];
    let EventFieldsLookup = datatable
(
    EventOriginalSubType: string,
    EventType: string,
    EventResult: string,
    DvcAction: string
)
[
    "login_success", "Logon", "Success", "Allowed",
    "login_failure", "Logon", "Failure", "Blocked",
    "login_challenge", "Logon", "", "",
    "login_verification", "Logon", "", "",
    "risky_sensitive_action_blocked", "Logon", "Failure", "Blocked",
    "riskay_sensitive_action_allowed", "Logon", "Success", "Allowed",
    "logout", "Logoff", "Success", "Allowed",
    "suspicious_login", "Logon", "Failure", "Blocked",
    "suspicious_login_less_secure_app", "Logon", "Failure", "Blocked",
    "suspicious_programmatic_login", "Logon", "Failure", "Blocked",
    "user_signed_out_due_to_suspicious_session_cookie", "Logoff", "Success", "Allowed"
];
    let ThreatEventTypes = dynamic(['suspicious_login', 'suspicious_login_less_secure_app', 'suspicious_programmatic_login', 'user_signed_out_due_to_suspicious_session_cookie']);
    let SupportedEventNames = EventFieldsLookup
        | project EventOriginalSubType;
    union isfuzzy=true  GoogleWorkspaceSchema, GWorkspace_ReportsAPI_login_CL
    | where not(disabled)
    //  -- Pre filtering
    | where 
        (isnull(starttime) or TimeGenerated >= starttime)
        and (isnull(endtime) or TimeGenerated <= endtime)
        and ((array_length(username_has_any) == 0) or actor_email_s has_any (username_has_any))
        and ((array_length(targetappname_has_any) == 0) or 'Google Workspace - login' in~ (targetappname_has_any))
        and ((array_length(srcipaddr_has_any_prefix) == 0) or (has_any_ipv4_prefix(IPAddress, srcipaddr_has_any_prefix)))
        and (array_length(srchostname_has_any) == 0) // SrcHostname not available in source
        and ((array_length(eventtype_in) == 0) or ("Logon" in~ (eventtype_in)) or ("Logoff" in~ (eventtype_in)))
        // eventresultdetails_in filtering done later in the parser
        // eventresult filtering done later in the parser
        and event_name_s in (SupportedEventNames)
    | lookup EventFieldsLookup on $left.event_name_s == $right.EventOriginalSubType
    // Filtering on 'eventresult' and eventtype_in
    | where (eventresult == "*" or (EventResult == eventresult))
        and ((array_length(eventtype_in) == 0) or (EventType in~ (eventtype_in)))
    | project-rename
        TargetUsername = actor_email_s,
        TargetUserId = actor_profileId_s,
        SrcIpAddr = IPAddress,
        LogonMethod = login_challenge_method_s,
        EventOriginalType = event_type_s,
        EventOriginalUid = id_uniqueQualifier_s
    // mapping ASimMatchingUsername
    | extend temp_isMatchTargetUsername=TargetUsername has_any(username_has_any)
    // ActorUsername not coming from source. Hence, not mapped.
    | extend ASimMatchingUsername = case
                                (
                                    array_length(username_has_any) == 0,
                                    "-",
                                    temp_isMatchTargetUsername,
                                    "TargetUsername",
                                    "No match"
                                )
    | extend
        TargetUsername = iif(event_name_s in (ThreatEventTypes), affected_email_address_s, TargetUsername),
        TargetUsernameType = _ASIM_GetUsernameType(TargetUsername),
        TargetUserIdType = iif(isnotempty(TargetUserId), "GWorkspaceProfileID", ""),
        EventSeverity = iif(event_name_s in (ThreatEventTypes), "High", "Informational")
    | extend 
        AdditionalFields = bag_pack
                  (
                      "Is_Suspicious",
                      is_suspicious_b,
                      "Is_Second_Factor_b",
                      is_second_factor_b,
                      "Logon_Type",
                      login_type_s,
                      "Sensitive_Action_Name",
                      sensitive_action_name_s
                  ),
        EventResult = case
              (
                  event_name_s in ('login_challenge', 'login_verification') and login_challenge_status_s == "passed",
                  "Success",
                  event_name_s in ('login_challenge', 'login_verification') and login_challenge_status_s == "incorrect_answer_entered",
                  "Failure",
                  EventResult
              ),
        EventResultDetails = iif(event_name_s in ('login_challenge', 'login_verification') and login_challenge_status_s == "incorrect_answer_entered", "MFA not satisfied", ""),
        RuleName = case
          (
              event_name_s == 'suspicious_login',
              "Google has detected a suspicious login for TargetUSerName",
              event_name_s == 'suspicious_login_less_secure_app',
              "Google has detected a suspicious login for TargetUSerName from a less secure app",
              event_name_s == 'suspicious_programmatic_login',
              "Google has detected a suspicious programmatic login for TargetUserName",
              event_name_s == 'user_signed_out_due_to_suspicious_session_cookie',
              "Suspicious session cookie detected for user TargetUserName",
              ""
          ),
        ThreatField = iif(event_name_s in (ThreatEventTypes), "TargetUserName", ""),
        ThreatFirstReportedTime = iif(event_name_s in (ThreatEventTypes), TimeGenerated, datetime(null)),
        ThreatLastReportedTime = iif(event_name_s in (ThreatEventTypes), TimeGenerated, datetime(null))
    // filtering on 'eventresultdetails_in'
    | where (array_length(eventresultdetails_in) == 0 or EventResultDetails in~ (eventresultdetails_in))
    | extend
        EventOriginalSubType = event_name_s,
        TargetAppName = "Google Workspace - login",
        Dst = "Google Workspace",
        Application = "Google Workspace",
        TargetAppType = "SaaS application",
        IpAddr = SrcIpAddr,
        User = TargetUsername,
        EventCount = int(1),
        EventStartTime = TimeGenerated,
        EventEndTime = TimeGenerated,
        EventProduct = "Workspace",
        EventVendor = "Google",
        Dvc="Workspace",
        EventSchema = 'Authentication',
        EventSchemaVersion = '0.1.3',
        EventUid = _ItemId
    | project-away 
        *_s,
        *_b,
        _ResourceId,
        Computer,
        MG,
        ManagementGroupName,
        RawData,
        SourceSystem,
        TenantId,
        temp*
};
parser
(
    starttime=starttime,
    endtime=endtime,
    username_has_any=username_has_any,
    targetappname_has_any=targetappname_has_any,
    srcipaddr_has_any_prefix=srcipaddr_has_any_prefix,
    srchostname_has_any=srchostname_has_any,
    eventtype_in=eventtype_in,
    eventresultdetails_in=eventresultdetails_in,
    eventresult=eventresult,
    disabled=disabled
)
}



