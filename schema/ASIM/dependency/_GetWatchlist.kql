
.create-or-alter function with (skipvalidation=true) _GetWatchlist(
    ['watchlistAlias']:string,
    ['keys']:dynamic=dynamic([])
)
{
    let function = (watchlists:string, keys:dynamic = dynamic([])) {
        Watchlist
        | where TimeGenerated < now()
        | where _DTItemType == 'watchlist-item'
        | where WatchlistAlias in (watchlists)
        | where array_length(keys) == 0 or SearchKey in (keys)
        | summarize hint.shufflekey=_DTItemId arg_max(_DTTimestamp, _DTItemStatus, LastUpdatedTimeUTC, SearchKey, WatchlistItem) by _DTItemId
        | where _DTItemStatus != 'Delete'
        | project-away _DTTimestamp, _DTItemStatus
    };
    function(watchlistAlias, keys)
}